use "classes";

import "util/common";
import "util/string";
import "util/template";
import "util/json";

function is_same_domain(src: String, dest: String): Boolean
{
	if (domain_ends_with(dest, "iggcdn.com")) return true;
	return false;
}

function adjust_document(request: Request, document: Element)
{
	var elem = document.query("html > head > meta[property='og:url'][content]");
	if (elem) {
		var value = elem.get_attr("content");
		var prefix = "https://www.indiegogo.com/projects/";
		if (value.starts_with(prefix)) {
			var project_id = value.substring(prefix.length);
			if (project_id.length > 0) {
				var data = extract_data(document);
				show_project_page(request, document, project_id, data);
			}
		}
	}
}

function @show_project_page(request: Request, document: Element, project_id: String, data: String[String])
{
	//log("project_id="+project_id);
	//dump(data.get_keys());
	var body = document.query("body");

	var page_title = document.query("html > head > title").extract_text();

	var json = JSON::parse(Http::get("https://www.indiegogo.com/private_api/campaigns/"+project_id+"/description").content);
	var desc_html = json.get("response").get("description_html").as_string();
	//body.set_html(desc_html);

	var campaign_data = JSON::parse_string(data["campaign"]);
	var title = campaign_data.get("title").as_string();
	var tagline = campaign_data.get("tagline").as_string();
	var owner_name = campaign_data.get("owner_name").as_string();
	var city = campaign_data.get("city").as_string();
	var country = campaign_data.get("country").as_string();
	var team_members = campaign_data.get("team_members");
	var owner_image: String;
	if (team_members.length() > 0) {
		var member = team_members.get(0);
		owner_image = member.get("avatar_url").as_string();
	}
	var currency_data = campaign_data.get("currency");
	var currency_code = currency_data.get("iso_code").as_string();
	var currency_symbol = currency_data.get("symbol").as_string();
	var raised = campaign_data.get("units_toward_goal", -1).as_int();
	var raised_percentage = campaign_data.get("goal_percentage", -1).as_int();
	var raised_goal = campaign_data.get("target_goal", -1).as_int();
	var funding_type = campaign_data.get("funding_type").as_string();
	var date_start = campaign_data.get("funding_started_at").as_string();
	var date_end = campaign_data.get("funding_ends_at").as_string();
	var category = campaign_data.get("category").get("name").as_string();
	var overview = campaign_data.get("overview").as_string();
	var overview_image = campaign_data.get("overview_image_url").as_string();
	//show_json(body, campaign_data);return;

	var perks_data = JSON::parse_string(data["perks"]);
	//show_json(body, perks_data);return;

	var tpl = Template::create("site/com/in/indiegogo.com/project.tpl");
	tpl.set("page_title", html_escape(page_title));
	tpl.set("title", html_escape(title));
	tpl.set("tagline", html_escape(tagline));
	tpl.set("owner_name", html_escape(owner_name));
	tpl.set("owner_location", html_escape(city+", "+country));
	if (owner_image) {
		tpl.set("owner_image", html_escape(owner_image));
	}
	tpl.set("currency_code", html_escape(currency_code));
	tpl.set("currency_symbol", html_escape(currency_symbol));
	tpl.set("raised", price_to_string(raised, currency_symbol));
	tpl.set("raised_goal", price_to_string(raised_goal, currency_symbol));
	tpl.set("raised_percentage", {raised_percentage});
	tpl.set("funding_type", html_escape(funding_type));
	tpl.set("date_start", html_escape(date_start.replace_char('T', ' ')));
	tpl.set("date_end", html_escape(date_end.replace_char('T', ' ')));
	tpl.set("overview", html_escape(overview));
	tpl.set("overview_image", html_escape(overview_image));
	tpl.set("description", desc_html);
	//tpl.set("description", "description");

	tpl.begin("perks");
	foreach (var perk in perks_data.as_array()) {
		var image = "https://c1.iggcdn.com/indiegogo-media-prod-cld/image/upload/c_fill,h_506,w_726/"+perk.get("perk_image_public_id").as_string()+".jpg";
		var price = perk.get("amount", -1).as_int();
		var label = perk.get("label").as_string();
		var delivery = get_date_only(perk.get("estimated_delivery_date").as_string());
		var number_claimed = perk.get("number_claimed", -1).as_int();
		var number_available = perk.get("number_available", -1).as_int();
		tpl.set("image", html_escape(image));
		tpl.set("price", price_to_string(price, currency_symbol));
		tpl.set("label", html_escape(label));
		tpl.set("delivery", html_escape(delivery));
		tpl.set("number_claimed", {number_claimed});
		tpl.set("number_available", {number_available});
		tpl.next_record();
	}
	tpl.end();

	request.replace_document(Document::parse(tpl.process()));
}

function @extract_data(document: Element): String[String]
{
	foreach (var script in document.query_all("script")) {
		var text = script.extract_text();
		if (text.contains("window.gon={};")) {
			return parse_list(text);
		}
	}
	return null;
}

function @parse_list(s: String): String[String]
{
	var result: String[String] = {};
	var off = 0;
	var key: String;
	for (;;) {
		var idx = json_aware_search_string(s, "gon.", off);
		if (idx == -1) break;

		if (key) {
			result[key] = strip_off_tail(s.substring(off, idx));
		}

		var idx2 = s.search_char('=', idx+4);
		if (idx2 == -1) break;

		key = s.substring(idx+4, idx2);
		off = idx2+1;
	}
	if (key) {
		result[key] = strip_off_tail(s.substring(off));
	}
	return result;
}

function @strip_off_tail(s: String): String
{
	var idx = s.rev_search_char(';');
	if (idx != -1) {
		s.set_length(idx);
	}
	return s;
}

function @json_aware_search_string(s: String, search: String, off: Integer): Integer
{
	var len1 = s.length;
	var len2 = search.length;
	if (len2 == 0) return -1;

	var c0 = search[0];

	for (var i=off; i<len1; i++) {
		if (s[i] == '"') {
			for (; i<len1; i++) {
				if (s[i] == '\\') {
					i++;
					continue;
				}
				if (s[i] == '"') {
					break;
				}
			}
			continue;
		}
		if (s[i] == c0 && len1-i >= len2) {
			var found = true;
			for (var j=1; j<len2; j++) {
				if (s[i+j] != search[j]) {
					found = false;
					break;
				}
			}
			if (found) {
				return i;
			}
		}
	}
	return -1;
}

function @price_to_string(price: Integer, symbol: String): String
{
	var s = {price};
	for (var i=s.length-3; i>0; i-=3) {
		s.insert_char(i, ',');
	}
	return html_escape(symbol)+s;
}

function @get_date_only(date: String): String
{
	var idx = date.search_char('T');
	if (idx != -1) {
		return date.substring(0, idx);
	}
	return date;
}
