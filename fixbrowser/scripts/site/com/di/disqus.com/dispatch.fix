use "classes";

import "util/common";
import "util/string";

function embed_disqus(request: Request, document: Element, thread_elem: Element, shortname: String, page_url: String, title: String)
{
	if (!title) {
		var title_elem = document.query("html > head > title");
		title = title_elem? title_elem.extract_text() : "";
	}
	if (!page_url) {
		page_url = request.get_url();
	}
	var url = Link::with_params("https://"+shortname+".disqus.com/embed", {"url": page_url, "title": title});
	thread_elem.set_html("<iframe src=\""+make_same_domain_link(request, url)+"\" style=\"width: 100%; height: 75vh\" autoresize=\"height\" frameborder=\"0\"></iframe>");
}

function adjust_document(request: Request, document: Element)
{
	var thread_elem = document.query("div#disqus_thread");
	var script_elem = thread_elem.get_next_tag();
	if (script_elem) {
		var noscript_elem = script_elem.get_next_tag();
		if (noscript_elem) {
			noscript_elem.remove();
		}
		var script = script_elem.extract_text();
		var shortname = extract_shortname(script);
		if (!shortname) {
			shortname = extract_shortname2(script);
		}
		if (shortname) {
			embed_disqus(request, document, thread_elem, shortname, null, null);
		}
	}
	else {
		foreach (var elem in document.query_all("script")) {
			var script = elem.extract_text();
			var shortname = extract_shortname(script);
			if (!shortname) {
				shortname = extract_shortname2(script);
			}
			if (shortname) {
				embed_disqus(request, document, thread_elem, shortname, null, null);
				break;
			}
		}
	}
}

function @extract_shortname(s: String): String
{
	var idx = s.search_string("disqus_shortname");
	if (idx == -1) return null;

	var start = s.search_multi_char("\'\"", idx);
	if (start == -1) return null;
	
	var end = s.search_multi_char("\'\"", start+1);
	if (end == -1) return null;

	return s.substring(start+1, end);
}

function @extract_shortname2(s: String): String
{
	// s.src = '//' + "shortname" + '.disqus.com/embed.js';

	var end = s.search_string(".disqus.com/embed.js");
	if (end == -1) return null;

	var start = s.rev_search_char('/', 0, end);
	if (start == -1) return null;

	var result = {""};
	for (var i=start; i<end; i++) {
		switch (s[i]) {
			case 'a'..'z', 'A'..'Z', '0'..'9', '-', '_':
				result[] = s[i];
				break;
		}
	}
	return result.length > 0? result : null;
}
