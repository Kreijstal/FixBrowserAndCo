use "classes";

import "util/common";
import "util/string";
import "util/template";
import "util/json";
import "util/extract";

function is_same_domain(src: String, dest: String): Boolean
{
	if (domain_ends_with(dest, "disquscdn.com")) return true;
	return false;
}

function handle_request(request: Request)
{
	if (request.get_path() == "/embed") {
		var shortname = request.get_domain();
		if (shortname.ends_with(".disqus.com")) {
			shortname = shortname.substring(0, shortname.length-11);
		}
		else {
			shortname = null;
		}

		var url = request.get_param("url");
		var title = request.get_param("title");

		if (request.get_param("load") == "1") {
			//var version = retrieve_version(shortname);
			var disqus_url = Link::with_params("https://disqus.com/embed/comments/", {
				"base": "default",
				"f": shortname,
				"t_u": url,
				"t_d": title,
				"t_t": title,
				"s_o": "default"
			});
			request.replace_with_html(show_comments(disqus_url));
			return;
		}

		var tpl = Template::create("site/com/di/disqus.com/confirm_load.tpl");
		tpl.set("shortname", html_escape(shortname));
		tpl.set("url", html_escape(url));
		tpl.set("title", html_escape(title));
		request.replace_with_html(tpl.process());
		return;
	}
}

function @show_comments(url: String): String
{
	var doc = Document::parse_from_http(url);
	var forum_data = JSON::parse_string(doc.query("script#disqus-forumData").extract_text());
	var thread_data = JSON::parse_string(doc.query("script#disqus-threadData").extract_text());

	var response = thread_data.get("response");
	var posts = response.get("posts").as_array();

	var tpl = Template::create("site/com/di/disqus.com/posts.tpl");

	var levels: Integer[String] = {};

	tpl.begin("posts");
	foreach (var post in posts) {
		var id = post.get("id").as_string();
		var parent = post.get("parent").as_string();
		//var message = post.get("message").as_string();
		var message = post.get("raw_message").as_string();
		var date = post.get("createdAt").as_string();
		var idx = date.search_char('T');
		if (idx != -1) {
			date[idx] = ' ';
		}

		var author = post.get("author");
		var profile_url = author.get("profileUrl").as_string();
		var name = author.get("name").as_string();

		var avatar = author.get("avatar");
		var image = avatar.get("large").get("cache").as_string();

		var level = parent? levels.get(parent, -1) + 1 : 0;
		levels[id] = level;

		tpl.set("message", html_escape(message));
		tpl.set("date", html_escape(date));
		tpl.set("profile_url", html_escape(profile_url));
		tpl.set("name", html_escape(name));
		tpl.set("image", html_escape(image));
		tpl.set("indent", {(level*56)});
		tpl.next_record();
	}
	tpl.end();

	return tpl.process();
}

function @retrieve_version(shortname: String): String
{
	var url = "https://"+shortname+".disqus.com/embed.js";
	var content = String::from_utf8(Http::get(url).content);

	var version = extract_string(content, "lounge.load.", ".js");
	if (version) {
		return version;
	}

	log("disqus: cannot retrieve version, using fallback");
	return "bc375ca08174aafcdafffefdfe6bc380";
}
