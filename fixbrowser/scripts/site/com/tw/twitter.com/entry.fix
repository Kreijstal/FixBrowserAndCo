use "classes";

import "util/common";
import "util/string";
import "util/template";

function is_same_domain(src: String, dest: String): Boolean
{
	if (domain_ends_with(dest, "twimg.com")) return true;
	return false;
}

function handle_request(request: Request)
{
	var domain = request.get_domain();
	if (domain == "pic.twitter.com") {
		return;
	}
	if (domain != "twitter.com" || request.get_scheme() != "https") {
		request.set_redirect("https://twitter.com"+request.get_path());
		return;
	}

	var path = request.get_path();
	var parts = path.split('/');
	if (parts.length >= 4 && parts[2] == "status") {
		if (parts.length > 4) {
			parts.set_length(4);
			request.set_redirect("https://twitter.com"+String::join(parts, "/"));
			return;
		}
		request.replace_with_html(show_status(request, parts[1]));
		return;
	}
}

function adjust_document(request: Request, document: Element)
{
	if (request.get_domain() == "pic.twitter.com") {
		var refresh = document.query("meta[http-equiv='refresh']").get_attr("content");
		request.set_redirect(refresh.substring(6));
		return;
	}

	remove_element(document, "form.NoScriptForm");

	foreach (var link in document.query_all("a[href^='https://t.co/'][title^='http']")) {
		link.set_attr("href", link.get_attr("title"));
	}
}

function @show_status(request: Request, twitter_id: String): String
{
	var s = {""};

	var doc = Document::parse_from_http(request.get_url());

	var tpl = Template::create("site/com/tw/twitter.com/list.tpl");
	tpl.set("title", twitter_id);

	tpl.begin("posts");
	foreach (var elem in doc.query_all("div.js-tweet-text-container")) {
		var parent = elem.get_parent();
		var text = elem.extract_text();
		var time = get_time(parent.query("small.time > a"));
		var full_name = parent.query("strong.fullname").extract_text();
		var username = parent.query("span.username > b").extract_text();
		var image = null;

		var next_elem = elem.get_next_tag();
		if (next_elem) {
			var photo_elem = next_elem.query("div.AdaptiveMedia-container div.AdaptiveMedia-photoContainer[data-image-url]");
			if (photo_elem) {
				image = photo_elem.get_attr("data-image-url");
			}
		}

		tpl.set("text", html_escape(text));
		tpl.set("time", html_escape(time));
		tpl.set("full_name", html_escape(full_name));
		tpl.set("username", html_escape(username));
		if (image) {
			tpl.set("image", html_escape(image));
		}
		tpl.next_record();
	}
	tpl.end();

	return tpl.process();
}

function @get_time(elem: Element): String
{
	return elem.get_attr("title");
}
