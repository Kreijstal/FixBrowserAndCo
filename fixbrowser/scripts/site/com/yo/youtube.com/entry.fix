use "classes";

import "util/common";
import "util/string";
import "util/extract";
import "util/json";
import "util/template";

function is_same_domain(src: String, dest: String): Boolean
{
	if (domain_ends_with(dest, "ytimg.com")) return true;
	if (domain_ends_with(dest, "youtube-nocookie.com")) return true;
	if (domain_ends_with(dest, "youtu.be")) return true;
	if (domain_ends_with(dest, "yt.be")) return true;
	if (domain_ends_with(dest, "googleusercontent.com")) return true;
	return false;
}

function log(value)
{
	if (!is_string(value)) {
		value = to_string(value);
	}
	else {
		value = {value};
	}
	for (var i=0; i<length(value); i++) {
		switch (value[i]) {
			case 32 .. 126, '\n':
				continue;

			default:
				value[i] = '?';
		}
	}
	@log(value);
}

function dump(value)
{
	log(to_string(value, true));
}

/*
function handle_request(request: Request)
{
	var path = request.get_path();
	if (path.search_char('.') == -1) {
		if (request.get_param("disable_polymer") == null) {
			var url = Link::with_params(request.get_url(), {"disable_polymer": "1"});
			request.set_redirect(url);
			return;
		}
	}
}
*/

function @parse_initial_data(data: JSON): String[String][]
{
	var contents = data.get("contents");
	var renderer = contents.get("twoColumnBrowseResultsRenderer");
	var tabs = renderer.get("tabs").as_array();
	var videos: String[String][] = [];
	//log("num_tabs="+tabs.length);
	foreach (var tab in tabs) {
		var renderer2 = tab.get("tabRenderer");
		var content = renderer2.get("content");
		var renderer3 = content.get("richGridRenderer");
		var contents2 = renderer3.get("contents");
		parse_contents(contents2, videos);
	}
	return videos;
}

function @parse_contents(data: JSON, videos: String[String][])
{
	for (var i=0; i<data.length(); i++) {
		var content = data.get(i);
		//log("keys="+content.get_keys());
		var item = content.opt("richItemRenderer");
		if (item.has_value()) {
			parse_item_renderer(item, videos);
		}
		var section = content.opt("richSectionRenderer");
		if (section.has_value()) {
			parse_section_renderer(section, videos);
		}
	}
}

function @parse_item_renderer(data: JSON, videos: String[String][])
{
	var content = data.get("content");
	var renderer = content.get("videoRenderer");
	parse_video(renderer, videos);
}

function @parse_section_renderer(data: JSON, videos: String[String][])
{
	var content = data.get("content");
	var renderer = content.get("richShelfRenderer");
	var title = renderer.get("title").get("runs").get(0).get("text");
	var contents = renderer.get("contents");
	parse_contents(contents, videos);
}

function @parse_video(data: JSON, videos: String[String][])
{
	var video_id = data.get("videoId").as_string();
	var title = data.get("title").get("runs").get(0).get("text").as_string();
	var thumbnail = data.get("thumbnail").get("thumbnails").get(0).get("url").as_string();
	videos[] = {
		"id": video_id,
		"title": title,
		"thumbnail": thumbnail
	};
}

function adjust_title_page(request: Request, document: Element)
{
	var videos: String[String][];

	foreach (var script in document.query_all("script")) {
		var code = script.extract_text();
		var short_code = code.length > 100? code.extract(0, 100) : code;
		//log("script="+short_code);
		
		var idx = short_code.search_string("window[\"ytInitialData\"] = ");
		if (idx != -1) {
			idx += "window[\"ytInitialData\"] = ".length;
			var idx2 = code.search_string("window[\"ytInitialPlayerResponse\"]", code.length-1000);
			if (idx2 != -1) {
				idx2 = code.rev_search_char(';', code.length-1000, idx2);
				if (idx2 != -1) {
					code = code.substring(idx, idx2);
					var data = json_parse(code);
					videos = parse_initial_data(data);
					//show_json(document.query("#search-container"), data);
				}
			}
		}
	}

	var tpl = Template::create("site/com/yo/youtube.com/title.tpl");
	tpl.set("data", html_escape(to_string(videos, true)));
	
	tpl.begin("videos");
	foreach (var video in videos) {
		tpl.set("id", video["id"]);
		tpl.set("url", "https://www.youtube.com/watch?v="+video["id"]);
		tpl.set("title", html_escape(video["title"]));
		tpl.set("thumbnail", video["thumbnail"]);
		tpl.next_record();
	}
	tpl.end();

	request.replace_document(Document::parse(template_process(tpl)));
}

function adjust_document(request: Request, document: Element)
{
	var path = request.get_path();
	if (path == "/") {
		adjust_title_page(request, document);
	}
}
