use "classes";

import "util/common";
import "util/string";
import "util/extract";

function handle_redirect(request: Request, new_url: String)
{
	if (new_url.starts_with("https://guce.yahoo.com/consent?")) {
		var cookie_GUCS, cookie_B;
		foreach (var cookie in request.get_headers().get("Set-Cookie", null)) {
			var value = extract_string(cookie, "GUCS=", ";");
			if (value) cookie_GUCS = value;
			value = extract_string(cookie, "B=", ";");
			if (value) cookie_B = value;
		}

		// https://guce.yahoo.com/consent?brandType=nonEu&gcrumb=XXX&done=https%3A%2F%2Fwww.yahoo.com%2F
		var http = Http::open(new_url);
		http.set_header("Cookie", "GUCS="+cookie_GUCS+"; B="+cookie_B);
		http.connect();
		var location = http.get_header("Location");
		http.close();

		// https://consent.yahoo.com/collectConsent?sessionId=XXX&lang=&inline=false
		var doc = Document::parse_from_http(location, null, {"Cookie": "GUCS="+cookie_GUCS+"; B="+cookie_B});
		var form = doc.query("form");

		var form_url = form.get_attr("action");
		form_url = URL::create(location).merge(URL::create(form_url)).to_string();

		var params: String[String] = {};
		foreach (var elem in form.query_all("input[type=hidden]")) {
			var name = elem.get_attr("name");
			var value = elem.get_attr("value");
			params[name] = value;
		}
		params["agree"] = "agree";

		http = Http::open(form_url);
		http.set_header("Cookie", "GUCS="+cookie_GUCS+"; B="+cookie_B);
		http.set_post_params(params);
		http.connect();
		location = http.get_header("Location");
		http.close();

		// https://guce.yahoo.com/copyConsent?sessionId=XXX
		http = Http::open(location);
		http.set_header("Cookie", "GUCS="+cookie_GUCS+"; B="+cookie_B);
		http.connect();
		var cookies = http.get_header_multiple("Set-Cookie");
		http.close();

		var cookie_EuConsent = null, cookie_GUC = null;
		foreach (var cookie in cookies) {
			var value = extract_string(cookie, "EuConsent=", ";");
			if (value) cookie_EuConsent = value;
			value = extract_string(cookie, "GUC=", ";");
			if (value) cookie_GUC = value;
		}

		request.replace_from_http(request.get_url(), null, {"Cookie": {
			"GUCS="+cookie_GUCS,
			"; B="+cookie_B,
			"; GUC="+cookie_GUC,
			"; EuConsent="+cookie_EuConsent
		}});
		return;
	}
}
