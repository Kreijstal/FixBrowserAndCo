use "classes";

import "util/common";
import "util/string";
import "util/json";
import "util/extract";

class @Post
{
	var id: String;
	var pid: String;
	var html: String;
	var children: Post[];
}

function is_same_domain(src: String, dest: String): Boolean
{
	if (domain_ends_with(dest, "fsdn.com")) return true;
	return false;
}

function adjust_document(request: Request, document: Element)
{
	var discussion_id = extract_discussion_id(document);
	if (discussion_id) {
		var comments = document.query("ul#commentlisting");
		var json = JSON::parse(Http::get("https://"+request.get_domain()+"/ajax.pl", {
			"op": "comments_fetch",
			"d2_seen": "1",
			"threshold": "-1",
			"highlightthresh": "-1",
			"discussion_id": discussion_id,
			"abbreviated": "",
			"pieces": "",
			"fetch_all": "1"
		}).content, false);
		var posts = extract_posts(json);
		var num_posts = posts[0] as Integer;
		comments.remove_all();
		remove_spam(posts[1] as Post[]);
		insert_posts(comments, posts[1] as Post[]);

		var loaded_span = document.query("span.loadedcommentcnt");
		if (loaded_span) {
			loaded_span.set_text({num_posts});
		}

		var noscript = comments.get_next_tag();
		if (noscript != null && noscript.get_type() == "noscript") { //XXX
			noscript.remove();
		}

		remove_element(document, "a#d2loadall");
	}
}

function @extract_discussion_id(document: Element): String
{
	foreach (var elem in document.query_all("script")) {
		var script = elem.extract_text();
		var discussion_id = extract_string(script, "D2.discussion_id(", ");");
		if (discussion_id) {
			return discussion_id;
		}
	}
	return null;
}

function @extract_posts(json: JSON): Dynamic[]
{
	var comment_html: String[String] = {};
	foreach (var key, value in json.get("html").as_object()) {
		comment_html[key] = value.as_string();
	}

	var update_data = json.get("update_data");
	var total_comments = update_data.get("totalcommentcnt", -1).as_int();
	var new_cids_order = update_data.get("new_cids_order");
	var new_cids_data = update_data.get("new_cids_data");

	var posts: Post[] = [];
	var posts_by_id: Post[String] = {};
	for (var i=0, n=new_cids_order.length(); i<n; i++) {
		var data = new_cids_data.get(i);

		var post = new Post;
		post.id = new_cids_order.get(i).as_long().to_string();
		post.pid = data.get("pid").as_long().to_string();
		post.html = comment_html.get("comment_"+post.id, "");
		posts[] = post;
		posts_by_id[post.id] = post;
	}

	var num_posts = posts.length;
	for (var i=0; i<posts.length; i++) {
		var post = posts[i];
		var parent = posts_by_id.get(post.pid, null);
		if (parent) {
			if (!parent.children) {
				parent.children = [];
			}
			parent.children[] = post;
			posts.remove(i--);
			continue;
		}
	}
	return [num_posts, posts];
}

function @remove_spam(posts: Post[])
{
	for (var i=0; i<posts.length; i++) {
		var post = posts[i];
		var html = post.html;

		if (html.search_string("\"Real coders don't need either GUIs or automatic memory management. Delphi is dead\" <b>by BarbaraHudson</b>") != -1) {
			posts.remove(i--);
			continue;
		}

		if (html.search_string("itQMMMMMW6iiiiiiiitKMMMMMMMMMMKtiiiiicSMMMMMMMMHJi") != -1) {
			posts.remove(i--);
			continue;
		}

		if (post.children) {
			remove_spam(post.children);
		}
	}
}

function @insert_posts(container: Element, posts: Post[])
{
	foreach (var post in posts) {
		var li = Element::create("li");
		li.set_attr("id", "tree_"+post.id);
		li.set_attr("class", "comment full contain");

		var comment_div = Element::create("div");
		comment_div.set_attr("id", "comment_"+post.id);
		comment_div.set_attr("class", "cw");
		comment_div.set_html(post.html);
		li.append_child(comment_div);

		if (post.children) {
			var ul = Element::create("ul");
			li.set_attr("id", "commtree_"+post.id);
			insert_posts(ul, post.children);
			li.append_child(ul);
		}
		
		container.append_child(li);
	}
}
