use "classes";

import "util/common";
import "util/string";

function adjust_document(request: Request, document: Element)
{
	foreach (var elem in document.query_all("link[rel='stylesheet'][href]")) {
		var href = elem.get_attr("href");

		if (href.contains("purecss")) {
			if (href.starts_with("https://unpkg.com/purecss@1.")) {
				replace_css(elem, "pure-min-1.0.0.css");
			}
			else if (href.starts_with("https://unpkg.com/purecss@0.6.")) {
				replace_css(elem, "pure-min-0.6.2.css");
			}
			else if (href.starts_with("http://yui.yahooapis.com/pure/0.5.")) {
				replace_css(elem, "pure-min-0.5.0.css");
			}
			else {
				replace_css(elem, "pure-min-0.4.2.css");
			}
		}
		else if (href.contains("bootstrap")) {
			href = strip_bootstrap_prefix(href);
			if (!href) continue;

			if (href.starts_with("4.3.")) {
				replace_css(elem, "bootstrap.min-4.3.1.css");
			}
			else if (href.starts_with("4.2.")) {
				replace_css(elem, "bootstrap.min-4.2.1.css");
			}
			else if (href.starts_with("4.")) {
				replace_css(elem, "bootstrap.min-4.0.0.css");
			}
			else if (href.starts_with("3.4.")) {
				replace_css(elem, "bootstrap.min-3.4.1.css");
			}
			else if (href.starts_with("3.3.")) {
				replace_css(elem, "bootstrap.min-3.3.7.css");
			}
			else {
				replace_css(elem, "bootstrap.min-2.3.2.css");
			}
		}
		else if (href.contains("materialize")) {
			replace_css(elem, "materialize.min-1.0.0.css");
		}
	}
}

function @strip_prefix(url: String, prefixes: String[]): String
{
	foreach (var prefix in prefixes) {
		if (url.starts_with(prefix)) {
			return url.substring(prefix.length);
		}
	}
	return null;
}

function @strip_single_prefix(url: String, prefix: String): String
{
	if (url.starts_with(prefix)) {
		return url.substring(prefix.length);
	}
	return null;
}

function @strip_bootstrap_prefix(input_url: String): String
{
	var url = URL::create(input_url);
	var domain = url.get_host();
	var path = url.path_to_string();

	if (domain_ends_with(domain, "bootstrapcdn.com")) return strip_single_prefix(path, "/bootstrap/");
	if (domain == "ajax.aspnetcdn.com") return strip_single_prefix(path, "/ajax/bootstrap/");
	if (domain == "cdnjs.cloudflare.com") return strip_single_prefix(path, "/ajax/libs/twitter-bootstrap/");
	return null;
}

function @replace_css(elem: Element, name: String)
{
	var style = Element::create("style");
	var text = Element::create_text(resource_get_string("dispatch/css/"+name));
	style.append_child(text);
	elem.get_parent().insert_after(style, elem);
	elem.remove();
}
